
'ALLNONREC'


'ACTION' TURN TO,

TURN TO+X+S:
        TEST+S,(IS NOT  +X,TURN+S;);
                IS      +X,TURN+S;.


[    GLOBALIS MENNYISEGEK    ]

'CHECK'

'MACRO' 'POINTER' MINFORM=1000, MXFORM=3000,MINVALT = 0, MXVALT=150,
    MINAZ=1,MAXAZ=100.
'LIST' LFORM[MINFORM:MXFORM], LVALT[0:MXVALT],LAZ[MINAZ:MAXAZ].

'FLAG'BESZAKITAS,

'MACRO''POINTER'KARELTOLAS=300000,MAXKAR=300256,
'MACRO''POINTER'SZAMELTOLAS=4000000,FIXMERET = 5,

'POINTER' MAXFORM,MAXVALT,ELOJELKONV.
'POINTER' PVALT, PFORM,ELLENORZESI SZINT,KOVETESI SZINT,VALT BAZIS,XFORM,HIBASZINT.
'TERMINAL'
        OPERATORJEL,BALROL JOBBRA,JOBBROL BALRA,
        FONTJEL.
'TERMINAL' PONTJEL, CSILLAGJEL, PLUSZJEL,MINUSZJEL, NYITOJEL, VESSZOJEL,
           VEGZAROJEL, FINJEL, SZINT, LVALT BETELT, LFORM BETELT,
           SZINTJEL.

'MACRO''POINTER'
        MIN SZC = 998,
        MAX SZC =1030,
        MAX HELY = 32,
        X NAGY SZC=1031,
        MIN FFORM =1032,
        MIN HOSSZ =200 000,
        EGY HOSSZU =200 001,
        KETTO HOSSZU =200 002.

'MACRO''POINTER'
        LL MERET = 2,KLOZ ADMIN = 1,OP ADMIN = 2,MAX ADMIN = 2,
        KL MERET = 5,

'POINTER'
        XRSZ,PRSZ,URSZ,ARSZ,ALL,MELYSEG,VALT RSZ.
'TERMINAL'
        HIBAS UTASITAS,RSZ BETELT,HIBAS LITERAL, XJEL.
'TERMINAL'
        GENJEL.

'TERMINAL'
        KERDOJEL.
'TERMINAL'
        IDOPONT,MEGSZAKADT,VIGYAZAT,
        MPERC,KHIVAS,MEGHIVTA A,SORSZAMU KLOZT,RSZ CIMFELIRAT,ATUTALASJEL,
        KONSJEL,NILJEL,DEFINIALATLAN,NEMNUMERIKUS,NEMNUMKAR,
        NEMKARAKTER,NEMLITERAL,NEMKLOZ,NEMAZONOSITO,HOSSZUAZ,MEGHIUSULT,
        HIBAS TERM,IDOLEJART,TUL NAGY VISSZALEPES,SIKERULT,NEM FUZER.

'MACRO''POINTER'
        MIN STAND = 1000000,
        MIN RSZ   =   50000, MX RSZ =   51000,
        MAX VALT CIM= 499.
'MACRO''POINTER'
        MIN GEN KONS = 2 000 000,
        MX HIVAS    = 10 000.

'POINTER'
        PGEN KONS.

'FLAG'
        VEGE,INPUT LISTA.

'FLAG' NYOMTATORA,FELH NYOMTATORA,FELHKONZOLRA.
'LIST'
        LRSZ[MIN RSZ:MX RSZ].
'POINTER'
        MAX RSZ,KEZDO IDO,HIVASSZAMLALO,MAX HIVAS,UTOLSO SIKERTELEN,
       INP ROGZITES,RSTO SIZE,MAX SYMBCOUNT.
'NOCHECK'




    [    ALAPVETO ELJARASOK    ]



'ACTION' GET KLL, GET HIVO RSZ, GET HIVO LL,SIKER KOVETESE,
        GET URSZ,GET MELYSEG,GET HELYIGENY,
        GET SORSZAMA,GET ELLENORZES,GET LIT,KIERTEKEL,
        ATUTALASOK KEZDETE,VISSZADMIN,VISSZATERES,
        KOVKLOZ,KOVARG,GETNARG,STACK RSZ,VALT URIT.

GET SORSZAMA+KL+N-X:
        ADD+KL+3+X,GET+LFORM+X+N.
GET NARG+X+N-Z:
        SUBTR+X+1+Z,GET+LFORM+Z+N.

VALT URIT+X-Y:
        MAKE+Y+X,
            ISM: (EQUAL+Y+0;
                  SUBTR+Y+2+Y,INCR+PRSZ,PUT+L RSZ+ P RSZ+MARKER,INCR+PRSZ,:ISM).

GET HELYIGENY+KL+X:
        ADD+KL+2+X,GET+LFORM+X+X.

RSZ HELYIGENYLES+X-Z:
        ADD+P RSZ+X+Z,MAX+XRSZ+Z+XRSZ, LSEQ+Z+MAX RSZ;
        RSZ KITERJESZTES.

STACK RSZ+X:
        PUT+L RSZ+P RSZ+X,INCR+PRSZ.

KOV LITERAL:
        GET+LFORM+A LL+A LL,NONEQUAL+A LL+0.

KIKERES+KLL+JEL+HAT+HRSZ+AT-LIT-LL:
   ISM: NONEQUAL+KLL+0, GET+LFORM+KLL+LL, GET LIT+LL+LIT,
          (MARKED+JEL, 'NOT'MARKED+LIT, MAKE +AT+LIT,EGYEZO ARGSZAMU+AT+HAT;
           MARKED+LIT, 'NOT'MARKED+JEL, UNMARK+LIT+AT,EGYEZO ARGSZAMU+AT+HAT;
           KOV KLOZ +KLL,:ISM).

EGYEZO ARGSZAMU+AT+HAT-N:
        AZONOS FVEK+AT+HAT+N;
        EQUAL+AT+HAT.

KOV KLOZ+KLL:
        INCR+KLL, GET+LFORM +KLL+ KLL.

GET LIT+X+Y-Z:
        ADD+X+1+Z, GET+LFORM + Z + Y,

FVNEVE+AT+F:
        LSEQ+ MIN FORM + AT, LSEQ + AT + MAX FORM, GET + LFORM + AT + F.

VALT+Y:
        LESS+0+Y,LESS+Y+MAX VALT CIM.

KIERTEKEL +A1 +R1 +A2 +R2 -V2 -W2 -X:
        MAKE+R2+R1, MAKE+A2+A1,
          ISM: (VALT+A2, ADD+A2+R2+V2, ADD+V2+1+W2, GET+LRSZ+W2+X,
                   (MARKED+X;
                    GET+LRSZ+V2+A2,MAKE+R2+X,:ISM);
               ).

KOV ARG +X +Y:
        INCR+X, GET+LFORM+X+Y.

ATUTAL+V1+R1+A2+R2-X:
        NEM TARTALMAZZA1+V1+R1+A2+R2,
        ADD+V1+R1+X, PUT+L RSZ+X+A2, INCR+X, PUT+LRSZ+X+R2,
           (EQUAL+R1+ARSZ;
            STACK RSZ+X,RSZ HELYIGENYLES+1).

NEM TARTALMAZZA1+V1+R1+A2+R2:
        TEST+9,(NEM TARTALM+V1+R1+A2+R2);.

AZONOS FVEK+A3+A4+N-F3-F4-N1:
        FVNEVE+A3+F3,FVNEVE+A4+F4,EQUAL+F3+F4,
         GET NARG+A3+N,GET NARG+A4+N1,EQUAL+N+N1.

GET KLL+RSZ+X:
        GET+L RSZ+RSZ+X.

GET HIVO LL+RSZ+X-Y:
        ADD+RSZ+2+Y,GET+LRSZ+Y+X.

GET HIVO RSZ+RSZ+X-Y:
        ADD+RSZ+1+Y,GET+LRSZ+Y+X.

GET URSZ   +RSZ+X-Y:
        ADD+RSZ+3+Y,GET+LRSZ+Y+X.

GET MELYSEG +RSZ+X-Y:
        ADD+RSZ+4+Y,GET+LRSZ+Y+Y,UNMARK+Y+X.

TORLENDO KLOZ+RSZ-Y:
          ADD+RSZ+4+Y,GET+LRSZ+Y+Y,MARKED+Y.

GET ELLENORZES+KL+X-Y:
        ADD+KL+4+Y,GET+LFORM+Y+X.

ATUTALASOK KEZDETE+P+Q-X:
        GET KLL+P+X,GET HELYIGENY+X+X,ADD+P+X+Q.

KONSTANS+X:
        MARKED+X;
        LSEQ+MINTAG+X.

HIBA VOLT:
        TEST+1;IS+BESZAITAS.

VEGRE VEGE:
        EQUAL+ARSZ+0.

SZABANYOS+X:
       LESS+MIN STAND+X.

ADMIN+X-Y:
        GET HELYIGENY+X+Y, RSZ HELYIGENYLES+Y,INCR+MELYSEG,
          STACK RSZ+X, STACK RSZ+A RSZ,STACK RSZ+A LL,STACK RSZ+U RSZ,
          STACK RSZ+MELYSEG,SUBTR+PRSZ+FIXMERET+ARSZ, MAKE+U RSZ+A RSZ,MAKE+ ALL+X,
          SUBTR+Y+FIXMERET+Y,VALT URIT+Y.

VISSZATERES:
        SIKER KOVETESE,GET HIVO LL+ARSZ+A LL, GET HIVO RSZ+A RSZ+A RSZ,
        DECR+MELYSEG.

VISSZADMIN:
          MAKE+PRSZ+URSZ,GET URSZ+PRSZ+URSZ,GET HIVO LL+PRSZ+ALL,
          GET HIVO RSZ+PRSZ+ARSZ,GET MELYSEG+PRSZ+MELYSEG.

EGYSZERU VISSZALEPES-X:
        ATUTALASOK KEZDETE+URSZ+X,EQUAL+X+PRSZ,
           VISSZADMIN.




$
EGYESITHETO1+ A1 +R3 +A2 +R4 -A3 -A4 -F3 -F4 -N:
        KONSTANS+A1,(EQUAL+A1+A2);
        MAKE+A3+A1,MAKE+A4+A2,GET NARG+A1+N,
                   ISM:(LSEQ+N+1;
                        KOV ARG +A3 +F3, KOV ARG +A4 +F4, DECR + N,
                          EGYESITHETO+ F3+R3 +F4 +R4,:ISM)).
$

$HATEKONYSAGI MEGFONTOLASOKBOL: $

EGYESITHETO1+B1+R1+B2+R2-A1-A2-A3-A4-B3-B4-R3-R4-F3-F4-N-M:
        KONSTANS+B1,(EQUAL+B1+B2);
        MAKE+B3+B1,MAKE+B4+B2,GET NARG+B3+N,
         ISM:(LSEQ+N+1;
              KOV ARG+B3+A1,KOV ARG+B4+A2,DECR+N,
              KIERTEKEL+A1+R1+A3+R3,
              KIERTEKEL+A2+R2+A4+R4,
               (VALT+A3,
                   (EQUAL+A3+A4,EQUAL+R3+R4,:ISM;
                    ATUTAL+A3+R3+A4+R4,:ISM);
                VALT+A4,
                   (ATUTAL+A4+R4+A3+R3,:ISM);
                KONSTANS+A3,
                   (EQUAL+A3+A4,:ISM);
                AZONOS FVEK+A3+A4+M,
                  ISM1:(LSEQ+M+1,:ISM;
                        KOV ARG+A3+F3,KOV ARG+A4+F4,DECR+M,
                           EGYESITHETO+F3+R3+F4+R4,:ISM1))).



[BEOLVASO RESZ   ]


'ACTION'  KLOZFUZES,  ELOJELINTEZES, VALTKEZDES,TAROL,HIBAINTEZES, PUT KLL,
         SZAMKONVERTALAS,PUT SORSZAMA,RESZFELSZABADITAS,
        KLOZATNEZES,KLOZTOLDAS,KLOZSZAMOZAS,KONSTANSELINTEZES,
         KOMMENT,  BEALLITASOK,ELSO LITERAL FVNEVE,B HIBA,SORHUZAS,
        ELOJEL ELL,PONT ELL.

'ACTION'
        BEALLITASOK,FELSZABADITAS,KLOZFELSZABADITAS,
        ATOMFELSZABADITAS,LITFELSZABADITAS,
        ELOTTE SZABAD,UTANA SZABAD,
        SZC KAPCSOLASA,SZC TORLESE,GET HOSSZ,PUT HOSSZ,
        SZC FELVETELE ,SZC LISTA,KONZOLRA F SZERINT.

KLOZFELSZABADITAS+KL-LL:
        GET+LFORM+KL+LL,FELSZABADITAS+KL+KLMERET,LITFELSZABADITAS+LL.

LITFELSZABADITAS+LL-LIT-LL1:
    ISM: (EQUAL+LL+0;
          GET LIT+LL+LIT,UNMARK+LIT+LIT,ATOMFELSZABADITAS+LIT,
          MAKE+LL1+LL,GET+LFORM+LL+LL,FELSZABADITAS+LL1+LLMERET,:ISM).

'RS' ATOMFELSZABADITAS+A-F-N:
        FVNEVE+A+F,GET NARG+A+N,RESZFELSZABADITAS+A+N;.

'RS' RESZFELSZABADITAS